# -- Different tags --

# @tparam type name -> @param name type
s/@tparam\s+([^ ]+)\s+([^ ]+)/@param \2 \1/g
s/@treturn/@return/g
s/@(number|bool|string)(\[.*\])?\s+(\w+)/@param \3 \1\2/g

# -- Type aliases --
/@/s/bool\b/boolean/g

# -- Table --
s/\{\[(.*)\]=(.*),\.\.\.\}/table<\1,\2>/g

# -- Arrays --
# {a|b,...} -> (a|b)[]
s/\{(.*\|.*),\.\.\.\}/(\1)[]/g
# This one seems to be typo, but there are a few.
# {[a],...} -> a[]
s/\{\[(.*)\],\.\.\.\}/\1[]/g
# {a,...} -> a[]
s/\{(.*),\.\.\.\}/\1[]/g

# -- Optionals --

# @param bool[opt=false] foo -> @param bool? foo (Default: false)
/@param/s/\[opt=(.*)\]/? (Default: \1)/g
/@/s/\[opt\]/?/g

# -- Unsupported --
/@module/d